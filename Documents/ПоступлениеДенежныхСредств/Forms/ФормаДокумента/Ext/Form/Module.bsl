
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.АвторДокумента) Тогда 
		Объект.АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	Если Параметры.Свойство("Основание")
		И ЗначениеЗаполнено(Параметры.Основание)
		И ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.РеализацияТоваровИУслуг") Тогда 
		СтруктураОплаты = Документы.РеализацияТоваровИУслуг.ПроверитьОплатуДокумента(Параметры.Основание);
		СтатусОплаты = СтруктураОплаты.СтатусОплаты;
		
		Если СтатусОплаты = Перечисления.ВидыОплатыДокумента.ПолностьюОплачен Тогда 
			СообщениеПользователю = Новый СообщениеПользователю();
			СообщениеПользователю.Текст = "Данный документ уже полностью оплачен. Ввод дополнительного документа оплаты не требуется!";
			СообщениеПользователю.Сообщить();
			Отказ = Истина;
			Возврат;
		Иначе
			Объект.СуммаДокумента = СтруктураОплаты.ОсталосьОплатить;
			ОсталосьОплатить = СтруктураОплаты.ОсталосьОплатить;

		КонецЕсли;
	КонецЕсли;
	
	
	ЗаполнитьРеквизитыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	Плательщик = Объект.Плательщик;
	ВидОперации = Объект.ВидОперации;
	ТипДенежныхСредств = Объект.ТипДенежныхСредств;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Объект.ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Наличные;
		Объект.ВидОперации = Перечисления.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьРеквизитыФормы()

&НаКлиенте
Процедура УстановитьВидимость()
	СтрокаТипПлательщика = "";
	
	Если Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные") Тогда 
		Элементы.Касса.Видимость = Истина;
		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;
	ИначеЕсли Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные") Тогда 
		Элементы.Касса.Видимость = Ложь;
		Элементы.ЭквайринговыйТерминал.Видимость = Истина;
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника") Тогда 
		Элементы.ДоговорКонтрагента.Видимость = Ложь;
		Элементы.Плательщик.Видимость = Истина;
		Элементы.Касса.Видимость = Истина;
		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;
		Элементы.РасчетныйСчет.Видимость = Ложь;
		СтрокаТипПлательщика = "СправочникСсылка.Сотрудники";
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ПолучениеНаличныхВБанке") Тогда 
		Элементы.ДоговорКонтрагента.Видимость = Ложь;
		Элементы.Плательщик.Видимость = Истина;
		Элементы.Касса.Видимость = Истина;
		Элементы.ЭквайринговыйТерминал.Видимость = Ложь;
		Элементы.РасчетныйСчет.Видимость = Истина;
		СтрокаТипПлательщика = "СправочникСсылка.Банки";
	Иначе 
		Элементы.ДоговорКонтрагента.Видимость = Истина;
		Элементы.Плательщик.Видимость = Истина;
		Элементы.РасчетныйСчет.Видимость = Ложь;
		СтрокаТипПлательщика = "СправочникСсылка.Контрагенты";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТипПлательщика) Тогда 
		Массив = Новый Массив();
		Массив.Добавить(Тип(СтрокаТипПлательщика));
		ОписаниеТипаПлательщика = Новый ОписаниеТипов(Массив);
		Элементы.Плательщик.ОграничениеТипа = ОписаниеТипаПлательщика;
	КонецЕсли;
	
	Если Элементы.ДоговорКонтрагента.Видимость = Истина
		И ЗначениеЗаполнено(Объект.Плательщик) Тогда 
		Элементы.ДоговорКонтрагента.Видимость = ПроверитьВидимостьДоговораВЗависимостиОтТипаДоговора(Объект.Плательщик);
	КонецЕсли;
	
	
КонецПроцедуры // УстановитьВидимость()

&НаСервереБезКонтекста
Функция ПроверитьВидимостьДоговораВЗависимостиОтТипаДоговора(Плательщик)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Плательщик", Плательщик);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Ссылка,
	|	Контрагенты.ТипКонтрагента КАК ТипКонтрагента
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Плательщик";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если ЗначениеЗаполнено(Выборка.ТипКонтрагента) И
		Выборка.ТипКонтрагента = Перечисления.ТипыКонтрагентов.Клиент Тогда 
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
	
КонецФункции // ПроверитьВидимостьДоговораВЗависимостиОтТипаДоговора()

&НаКлиенте
Процедура ТипДенежныхСредствПриИзменении(Элемент)
	МассивОчищаемыхРеквизитов = Новый Массив;
	Если Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные")
		И ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал) Тогда 
		МассивОчищаемыхРеквизитов.Добавить("ЭквайринговыйТерминал");
		ТекстВопроса = "При изменении реквизита ""Тип оплаты"" на Наличные будут очищены следующие типы данных:
		| Эквайринговый терминал
		| Продолжить?";
	ИначеЕсли Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные")
		И ЗначениеЗаполнено(Объект.Касса) Тогда 
		МассивОчищаемыхРеквизитов.Добавить("Касса");
		ТекстВопроса = "При изменении реквизита ""Тип оплаты"" на Банковская карта будут очищены следующие типы данных:
		| Касса
		| Продолжить?";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииТипаОплаты", ЭтаФорма,МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, "Внимание!");
	Иначе
		ТипДенежныхСредств = Объект.ТипДенежныхСредств;
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииТипаОплаты(Результат, ДополнительныеПараметры) Экспорт 
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для Каждого Реквизит Из ДополнительныеПараметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		
		УстановитьВидимость();
		ТипДенежныхСредств = Объект.ТипДенежныхСредств;
	Иначе
		Объект.ТипДенежныхСредств = ТипДенежныхСредств;
	КонецЕсли;
	
КонецПроцедуры // ПослеОтветаНаВопросОбИзмененииТипаОплаты()

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
		МассивОчищаемыхРеквизитов = Новый Массив;
		
	Если (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ПолучениеНаличныхВБанке"))
		И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда 
		МассивОчищаемыхРеквизитов.Добавить("ДоговорКонтрагента");
		
		ТекстВопроса = "При изменении реквизита ""Вид Операции"" будут очищены следующие типы данных:
		| Договор Контрагента
		| Продолжить?";
		
	ИначеЕсли 
		 Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя")
		И (ЗначениеЗаполнено(Объект.ДоговорКонтрагента) ИЛИ ЗначениеЗаполнено(Объект.Плательщик)) Тогда 
		МассивОчищаемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивОчищаемыхРеквизитов.Добавить("Плательщик");
		
		ТекстВопроса = "При изменении реквизита ""Вид Операции"" будут очищены следующие типы данных:
		| Договор Контрагента
		| Плательщик
		| Продолжить?";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииВидаОперации", ЭтаФорма,МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, "Внимание!");
	Иначе
		ВидОперации = Объект.ВидОперации;
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииВидаОперации(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда 
		Для Каждого Реквизит Из ДополнительныеПараметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		
		УстановитьВидимость();
		ВидОперации = Объект.ВидОперации;
	Иначе
		Объект.ВидОперации = ВидОперации;
	КонецЕсли;
	
	
КонецПроцедуры // ПослеОтветаНаВопросОбИзмененииВидаОперации()

&НаКлиенте
Процедура ПлательщикПриИзменении(Элемент)
	ЭтоПоставщик = ПроверитьВидимостьДоговораВЗависимостиОтТипаДоговора(Объект.Плательщик);
	МассивОчищаемыхРеквизитов = Новый Массив;
	
	Если НЕ ЭтоПоставщик И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда 
		МассивОчищаемыхРеквизитов.Добавить("ДоговорКонтрагента");
		
		ТекстВопроса = ТекстВопроса = "При изменении реквизита ""Плательщик"" будут очищены следующие типы данных:
		| Договор Контрагента
		| Продолжить?";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивОчищаемыхРеквизитов) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбИзмененииПлательщика", ЭтаФорма,МассивОчищаемыхРеквизитов);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, "Внимание!");
	Иначе
		Плательщик = Объект.Плательщик;
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбИзмененииПлательщика(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда 
		Для Каждого Реквизит Из ДополнительныеПараметры Цикл
			Объект[Реквизит] = Неопределено;
		КонецЦикла;
		
		УстановитьВидимость();
		Плательщик = Объект.Плательщик;
	Иначе
		Объект.Плательщик = Плательщик;
	КонецЕсли;

	
КонецПроцедуры // ПослеОтветаНаВопросОбИзмененииПлательщика()

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	Если ОсталосьОплатить >0 И Объект.СуммаДокумента > ОсталосьОплатить Тогда
		Объект.СуммаДокумента = ОсталосьОплатить
	КонецЕсли;
	
КонецПроцедуры







                           

