
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(АвторДокумента) Тогда 
		АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	
КонецПроцедуры 

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ПоступлениеУслугНоменклатура.Сумма) КАК Сумма,
	|	ПоступлениеУслугНоменклатура.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ПоступлениеУслугНоменклатура.Ссылка КАК Ссылка,
	|	ПоступлениеУслугНоменклатура.Услуга.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	Документ.ПоступлениеУслуг.Номенклатура КАК ПоступлениеУслугНоменклатура
	|ГДЕ
	|	ПоступлениеУслугНоменклатура.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеУслугНоменклатура.СтатьяЗатрат,
	|	ПоступлениеУслугНоменклатура.Ссылка,
	|	ПоступлениеУслугНоменклатура.Услуга.СчетБухгалтерскогоУчета";
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИтоги.Следующий() Цикл
		ДвижениеЗатрат = Движения.УчетЗатрат.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеЗатрат, ВыборкаИтоги);
		ДвижениеЗатрат.Период = Дата;
		
		//Регистр Журнала Бухгалтерских проводок
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = ВыборкаИтоги.СчетБухгалтерскогоУчета;
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаИтоги.Сумма;
		Движение.Содержание = "Отражено поступление услуг от поставщика";
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, ВыборкаИтоги.СтатьяЗатрат);
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, Поставщик);
		
	КонецЦикла;
	
КонецПроцедуры


