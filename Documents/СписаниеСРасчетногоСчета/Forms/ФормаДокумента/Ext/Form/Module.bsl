
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры


&НаКлиенте
Процедура УстановитьВидимость()

	ЭтоСнятиеНаличныхВКассу = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписанияСРасчетногоСчета.СнятиеНаличныхВКассу"));
	ЭтоОплатаПоставщику = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписанияСРасчетногоСчета.ОплатаПоставщику"));
	ЭтоВозвратПокупателю = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписанияСРасчетногоСчета.ВозвратПокупателю"));
	КонтрагентЭтоПоставщик = КонтрагентЭтоПоставщик(Объект.Получатель);
	
	Элементы.ДоговорКонтрагента.Видимость = (ЭтоОплатаПоставщику ИЛИ ЭтоВозвратПокупателю) И КонтрагентЭтоПоставщик;
	Элементы.Касса.Видимость = ЭтоСнятиеНаличныхВКассу;
	Элементы.Получатель.Видимость = Не ЭтоСнятиеНаличныхВКассу;
	
	СтрокаТипПолучателя = "СправочникСсылка.Сотрудники";
	Если ЭтоОплатаПоставщику Или ЭтоВозвратПокупателю Тогда
		СтрокаТипПолучателя = "СправочникСсылка.Контрагенты";
	КонецЕсли;
	
	Массив = Новый Массив;
	Массив.Добавить(Тип(СтрокаТипПолучателя));
	
	ОписаниеТипаПолучателя = Новый ОписаниеТипов(Массив);
	Элементы.Получатель.ОграничениеТипа = ОписаниеТипаПолучателя;
	
КонецПроцедуры // УстановитьВидимость()

&НаСервереБезКонтекста
Функция КонтрагентЭтоПоставщик(КонтрагентСсылка)
	
	Если ТипЗнч(КонтрагентСсылка) = Тип("СправочникСсылка.Контрагенты") Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ТипКонтрагента = &ТипКонтрагентаКлиент
		|	И Контрагенты.Ссылка = &КонтрагентСсылка";
		
		Запрос.УстановитьПараметр("КонтрагентСсылка", КонтрагентСсылка);
		Запрос.УстановитьПараметр("ТипКонтрагентаКлиент", Перечисления.ТипыКонтрагентов.Клиент);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Возврат РезультатЗапроса.Пустой();
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // КонтрагентЭтоПоставщик()

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если не ЗначениеЗаполнено(Объект.АвторДокумента) Тогда 
		Объект.АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры







































